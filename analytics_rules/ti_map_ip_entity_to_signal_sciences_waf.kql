let dt_lookBack = 1h;
let ioc_lookBack = 14d;
let IP_Indicators = ThreatIntelIndicators
    | where isnotempty(ObservableValue)
    | where Pattern matches regex @"(\d{1,3}\.){3}\d{1,3}"
    | extend TI_IP = extract(@"(\d{1,3}\.){3}\d{1,3}", 0, Pattern)
    | where TimeGenerated >= ago(ioc_lookBack)
    | extend TI_ipEntity =  ObservableValue
    | where ipv4_is_private(TI_ipEntity) == false
        and TI_ipEntity !startswith "fe80"
        and TI_ipEntity !startswith "::"
        and TI_ipEntity !startswith "127."
    | summarize LatestIndicatorTime = arg_max(TimeGenerated, *) by Id
    | where IsActive == true and ValidUntil > now();
IP_Indicators
| join kind=innerunique (
    Signal_Sciences_CL
    | where timestamp_t >= ago(dt_lookBack)
    | where isnotempty(RemoteIp_s)
    | where ipv4_is_private(RemoteIp_s) == false
        and RemoteIp_s !startswith "fe80"
        and RemoteIp_s !startswith "::"
        and RemoteIp_s !startswith "127."
    | extend SigSci_TimeStamp = timestamp_t
    )
    on $left.TI_ipEntity == $right.RemoteIp_s
| where SigSci_TimeStamp < ValidUntil
| summarize SigSci_TimeStamp = arg_max(SigSci_TimeStamp, *) by Id, RemoteIp_s
| project
    timestamp = SigSci_TimeStamp,
    Id,
    Tags,
    ValidUntil,
    Confidence,
    TI_ipEntity,
    ObservableValue,
    Type