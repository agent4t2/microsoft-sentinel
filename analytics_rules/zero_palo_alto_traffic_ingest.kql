let total_log_threshold = 1;
let lookback = 1h;
let step = 15m;
let historical_window = 7d;
// Identify active firewalls to profile
let PaloFW = CommonSecurityLog 
  | where TimeGenerated > ago(1d) 
  | where tolower(Computer) startswith "fw" or tolower(Computer) startswith "palo" or tolower(Computer) contains "ngfw"
  | summarize by Computer;
// Build historical baseline per firewall (avg logs per bin)
let FirewallBaseline = CommonSecurityLog
  | where TimeGenerated between (ago(historical_window) .. ago(1d))
  | where DeviceVendor == "Palo Alto Networks"
  | where Activity in ("TRAFFIC")
  | where Computer in (PaloFW)
  | summarize bin_count = count() by Computer, bin(TimeGenerated, step)
  | summarize baseline_avg_logs_per_bin = avg(bin_count) by Computer;
// Evaluate recent log behavior per firewall
let LiveLogAnalysis = CommonSecurityLog
  | where TimeGenerated > ago(lookback)
  | where DeviceVendor == "Palo Alto Networks"
  | where Activity in ("TRAFFIC")
  | where Computer in (PaloFW)
  | make-series log_count = count() on TimeGenerated from ago(lookback + step) to now() step step by Computer
  | extend filtered_log_count = array_slice(log_count, 1, array_length(log_count))
  | mv-expand val = filtered_log_count
  | extend val = toint(val)
  | summarize 
      zero_bins = countif(val == 0), 
      total_logs = sum(val), 
      log_series = make_list(val) 
    by Firewall = Computer;
// Enrich with baseline and dynamic HArole tagging
LiveLogAnalysis
| join kind=leftouter (
    FirewallBaseline
    | project Firewall=Computer, baseline_avg_logs_per_bin
) on Firewall
| extend FW = tolower(Firewall)
| extend HArole = case(
    FW contains "01", "Primary",
    FW contains "02", "Standby",
    FW contains "fw1", "Primary",
    FW contains "fw2", "Standby",
    "Unknown")
// Final filtering logic
| where zero_bins >= 2 and baseline_avg_logs_per_bin > 0 and total_logs < (baseline_avg_logs_per_bin * (lookback / step) * 0.5)
// Compute percent drop and prep alert JSON
| extend 
    ingest_vs_baseline_pct = round(100.0 * total_logs / (baseline_avg_logs_per_bin * (lookback / step)), 1),
    LastChecked = now(),
    AlertDetails = pack_all()
// Final output
| project LastChecked, Firewall, HArole, zero_bins, total_logs, baseline_avg_logs_per_bin, ingest_vs_baseline_pct, log_series, AlertDetails
| order by ingest_vs_baseline_pct asc
