let sqli_patterns = dynamic([
    "admin' --", "admin' #", "admin'/*", 
    "0=1", "1=0", "1=1", "1=2", 
    "' or 1=1--", "' or 1=1#", "' or 1=1/*", 
    "') or '1'='1--", "') or ('1'='1--"
]);
oraudit_CL
| extend auth_raw = tostring(AUTHENTICATION_TYPE)
| extend 
    AuthType = extract(@"\(TYPE=\((.*?)\)\)", 1, auth_raw),
    Protocol = extract(@"\(PROTOCOL=(.*?)\)", 1, auth_raw),
    ClientIP = extract(@"\(HOST=(.*?)\)", 1, auth_raw),
    Port = extract(@"\(PORT=(.*?)\)", 1, auth_raw),
    ClientAddress = iif(auth_raw has "(CLIENT ADDRESS=())", "Empty", "Present")
| where isnotempty(DBUSERNAME)
| where isnotempty(DBNAME)
| extend
    MatchInSQLText = iff(SQL_TEXT has_any (sqli_patterns), true, false),
    MatchInOSUser = iff(OS_USERNAME has_any (sqli_patterns), true, false),
    MatchInDBUser = iff(DBUSERNAME has_any (sqli_patterns), true, false),
    MatchInUserHost = iff(USERHOST has_any (sqli_patterns), true, false),
    MatchInObjectName = iff(OBJECT_NAME has_any (sqli_patterns), true, false),
    MatchInObjectSchema = iff(OBJECT_SCHEMA has_any (sqli_patterns), true, false)
| where MatchInSQLText or MatchInOSUser or MatchInDBUser or MatchInUserHost or MatchInObjectName or MatchInObjectSchema
| project 
    TimeGenerated, 
    USERHOST, OS_USERNAME, DBUSERNAME, SQL_TEXT, 
    Computer, DBNAME, ACTION_NAME, OBJECT_NAME, OBJECT_SCHEMA, 
    ClientIP, AuthType, Protocol, Port, ClientAddress,
    MatchInSQLText, MatchInOSUser, MatchInDBUser, MatchInUserHost, MatchInObjectName, MatchInObjectSchema
